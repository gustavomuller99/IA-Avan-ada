(define (domain vampire)
    (:requirements :conditional-effects)
    (:predicates
        (light-on ?r)
        (slayer-is-alive)
        (slayer-is-in ?r)
        (vampire-is-alive)
        (vampire-is-in ?r)
        (fighting)
        ;
        ; static predicates
        (NEXT-ROOM ?r ?rn)
        (CONTAINS-GARLIC ?r)
    )

    (:action toggle-light
        :parameters (?anti-clockwise-neighbor ?room ?clockwise-neighbor)
        :precondition (and
            (NEXT-ROOM ?anti-clockwise-neighbor ?room)
            (NEXT-ROOM ?room ?clockwise-neighbor)
            (not (fighting))
        )
        :effect (and
            (when 
                (and (light-on ?room) 
                    (not (slayer-is-in ?room))
                )
                (not (light-on ?room))
            )
            (when 
                (and (light-on ?room)
                    (slayer-is-in ?room)
                    (light-on ?clockwise-neighbor)
                    (not (vampire-is-in ?clockwise-neighbor))
                )
                (and 
                    (not (light-on ?room))
                    (not (slayer-is-in ?room))
                    (slayer-is-in ?clockwise-neighbor)
                )
            )
            (when 
                (and (light-on ?room)
                    (slayer-is-in ?room)
                    (light-on ?clockwise-neighbor)
                    (vampire-is-in ?clockwise-neighbor)
                )
                (and 
                    (not (light-on ?room)) 
                    (not (slayer-is-in ?room))
                    (slayer-is-in ?clockwise-neighbor)
                    (fighting)
                )
            )
            (when 
                (and (light-on ?room)
                    (slayer-is-in ?room)
                    (not (light-on ?clockwise-neighbor))
                    (not (vampire-is-in ?anti-clockwise-neighbor))
                )
                (and 
                    (not (light-on ?room))
                    (not (slayer-is-in ?room))
                    (slayer-is-in ?anti-clockwise-neighbor)
                )
            )
            (when 
                (and (light-on ?room)
                    (slayer-is-in ?room)
                    (not (light-on ?clockwise-neighbor))
                    (vampire-is-in ?anti-clockwise-neighbor)
                )
                (and 
                    (not (light-on ?room))
                    (not (slayer-is-in ?room))
                    (slayer-is-in ?anti-clockwise-neighbor)
                    (fighting)
                )
            )
            (when
                (and (not (light-on ?room))
                    (not (vampire-is-in ?room))
                )
                (light-on ?room)
            )
            (when
                (and (not (light-on ?room))
                    (vampire-is-in ?room)
                    (not (light-on ?anti-clockwise-neighbor))
                    (not (slayer-is-in ?anti-clockwise-neighbor))
                )
                (and (light-on ?room)
                    (not (vampire-is-in ?room))
                    (vampire-is-in ?anti-clockwise-neighbor)
                )
            )
            (when
                (and (not (light-on ?room))
                    (vampire-is-in ?room)
                    (not (light-on ?anti-clockwise-neighbor))
                    (slayer-is-in ?anti-clockwise-neighbor)
                )
                (and (light-on ?room)
                    (not (vampire-is-in ?room))
                    (vampire-is-in ?anti-clockwise-neighbor)
                    (fighting)
                )
            )
            (when
                (and (not (light-on ?room))
                    (vampire-is-in ?room)
                    (light-on ?anti-clockwise-neighbor)
                    (not (slayer-is-in ?clockwise-neighbor))
                )
                (and (light-on ?room)
                    (not (vampire-is-in ?room))
                    (vampire-is-in ?clockwise-neighbor)
                )
            )
            (when
                (and (not (light-on ?room))
                    (vampire-is-in ?room)
                    (light-on ?anti-clockwise-neighbor)
                    (slayer-is-in ?clockwise-neighbor)
                )
                (and (light-on ?room)
                    (not (vampire-is-in ?room))
                    (vampire-is-in ?clockwise-neighbor)
                    (fighting)
                )
            )
        )
    )

    (:action watch-fight
        :parameters (?room)
        :precondition (and
            (slayer-is-in ?room)
            (slayer-is-alive)
            (vampire-is-in ?room)
            (vampire-is-alive)
            (fighting)
        )
        :effect (and
            (when (or (light-on ?room) (CONTAINS-GARLIC ?room))
                (and (not (vampire-is-alive)) (not (fighting))))
            (when (and (not (light-on ?room)) (not (CONTAINS-GARLIC ?room)))
                (and (not (slayer-is-alive)) (not (fighting))))
        )
    )
)
